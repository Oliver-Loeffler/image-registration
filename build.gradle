// ROOT PROJECT
plugins {
   id "java"
   id "jacoco"
   id "com.github.hierynomus.license-base" version "0.16.1"
   id "org.sonarqube" version "6.0.1.5171"
   id "de.jjohannes.extra-java-module-info" version "0.16"
   id 'net.raumzeitfalle.gradle.gocdversion' version '0.3.9'
}

allprojects {
    repositories {
    	mavenLocal()
        mavenCentral()
    }

    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'org.sonarqube'
    apply plugin: 'com.github.hierynomus.license-base'
    apply plugin: 'signing'
    apply plugin: 'project-report'

    group = 'net.raumzeitfalle.registration'

    gocdVersion {
        appendComputerNameToLocalBuilds = false
        appendTimestampToLocalBuilds = false
    }

    java {
    	sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
	    withJavadocJar()
    	withSourcesJar()
    }

    jacocoTestReport {
        reports {
            xml.required = true
        }
    }

    test {
        finalizedBy jacocoTestReport
    }
    	
    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.4'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.11.4'
        testRuntimeOnly    'org.junit.jupiter:junit-jupiter-engine:5.11.4'
    }
	
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    sonarqube {
        properties {
            property "sonar.host.url", "https://sonarcloud.io"
            property "sonar.projectKey", System.getenv('SONARCLOUD_PROJECT_KEY')
            property "sonar.login", System.getenv('SONARCLOUD_LOGIN')
            property "sonar.organization", System.getenv('SONARCLOUD_ORG')
            property 'sonar.coverage.jacoco.xmlReportPaths', "./solver-test/build/reports/jacoco/testCodeCoverageReport/testCodeCoverageReport.xml"
        }
    }
	
    htmlDependencyReport {
    	projects = project.allprojects
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.BIN
    gradleVersion = "8.12.1"
}
